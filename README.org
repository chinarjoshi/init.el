#+title: Emacs Config
#+property: header-args:emacs-lisp :tangle config.el

#+begin_quote
/It's a great operating system, lacking only a decent editor/
#+end_quote
* Package Setup
#+begin_src emacs-lisp
;;; config.el --- Emacs config (tangled from README.org) -*- lexical-binding: t -*-

;;; Package setup
(require 'package)
(add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/") t)
(package-initialize)
(unless package-archive-contents
  (package-refresh-contents))

(setq inhibit-startup-echo-area-message "c")
#+end_src

* Basics
#+begin_src emacs-lisp
;;; Backups and autosave
(setq make-backup-files nil)
(setq auto-save-default nil)
(setq create-lockfiles nil)

;;; Use y/n instead of yes/no
(setq use-short-answers t)

;;; Show filename instead of key echo
(setq echo-keystrokes 0)

;;; Smooth scrolling
(setq scroll-conservatively 101)
(setq scroll-margin 2)
(setq fast-but-imprecise-scrolling t)

;;; Don't prompt about unsaved buffers on quit
(setq confirm-kill-processes nil)
(setq confirm-kill-emacs nil)
(advice-add 'save-buffers-kill-terminal :before
            (lambda (&rest _)
              (dolist (buf (buffer-list))
                (with-current-buffer buf
                  (set-buffer-modified-p nil)))))
#+end_src

* UI
#+begin_src emacs-lisp
;;; UI
(add-hook 'prog-mode-hook 'display-line-numbers-mode)

;;; No modeline
(setq-default mode-line-format nil)

(setq modus-themes-common-palette-overrides
      '((bg-main "#000000")
        (bg-dim "#0a0a0a")
        (bg-alt "#0a0a0a")))
(load-theme 'modus-vivendi t)
(dolist (face '(fringe line-number line-number-current-line))
  (set-face-attribute face nil :background "#000000"))
(setf (cdr (assq 'continuation fringe-indicator-alist)) '(nil nil))
(setf (cdr (assq 'truncation fringe-indicator-alist)) '(nil nil))
#+end_src

* Fonts
#+begin_src emacs-lisp
;;; Fonts
(defun my/set-fonts ()
  (set-face-attribute 'default nil :family "Inconsolata Nerd Font" :height 130)
  (set-face-attribute 'fixed-pitch nil :family "Inconsolata Nerd Font")
  (set-face-attribute 'variable-pitch nil :family "Lora"))

(if (daemonp)
    (add-hook 'after-make-frame-functions
              (lambda (frame)
                (with-selected-frame frame
                  (my/set-fonts))))
  (add-hook 'after-init-hook #'my/set-fonts))
#+end_src

* Org
#+begin_src emacs-lisp
;;; Org
(add-hook 'org-mode-hook 'variable-pitch-mode)
(add-hook 'org-mode-hook 'auto-save-mode)
(custom-set-faces
 '(org-block ((t (:inherit fixed-pitch))))
 '(org-block-begin-line ((t (:inherit fixed-pitch))))
 '(org-block-end-line ((t (:inherit fixed-pitch))))
 '(org-code ((t (:inherit fixed-pitch))))
 '(org-meta-line ((t (:inherit fixed-pitch))))
 '(org-table ((t (:inherit fixed-pitch))))
 '(org-verbatim ((t (:inherit fixed-pitch)))))
#+end_src

* Notes
#+begin_src emacs-lisp
;; Periodic notes (like Obsidian)
(defvar notes-directory "~/notes")

(defun notes--daily-file (time)
  (expand-file-name (format-time-string "daily/%m-%d.org" time) notes-directory))

(defun notes--weekly-file (time)
  (expand-file-name (format-time-string "weekly/%V.org" time) notes-directory))

(defun notes--monthly-file (time)
  (expand-file-name (format-time-string "monthly/%B.org" time) notes-directory))

(defun notes--ensure-dir (file)
  (let ((dir (file-name-directory file)))
    (unless (file-exists-p dir)
      (make-directory dir t))))

(defun notes-open-daily ()
  (interactive)
  (let* ((time (current-time))
         (file (notes--daily-file time))
         (day-name (format-time-string "%A" time))
         (new-file (not (file-exists-p file))))
    (notes--ensure-dir file)
    (find-file file)
    (when (and new-file (= (buffer-size) 0))
      (insert (format "* :%s:\n" day-name))
      (save-buffer))))

(defun notes-open-weekly ()
  (interactive)
  (let ((file (notes--weekly-file (current-time))))
    (notes--ensure-dir file)
    (find-file file)))

(defun notes-open-monthly ()
  (interactive)
  (let ((file (notes--monthly-file (current-time))))
    (notes--ensure-dir file)
    (find-file file)))

(defun notes--current-type ()
  (when buffer-file-name
    (let ((name (file-name-nondirectory buffer-file-name))
          (dir (file-name-nondirectory (directory-file-name (file-name-directory buffer-file-name)))))
      (cond
       ((string= dir "daily") 'daily)
       ((string= dir "weekly") 'weekly)
       ((string= dir "monthly") 'monthly)))))

(defun notes--parse-date ()
  (let ((name (file-name-sans-extension (file-name-nondirectory buffer-file-name)))
        (type (notes--current-type))
        (year (string-to-number (format-time-string "%Y"))))
    (pcase type
      ('daily (let ((parts (split-string name "-")))
                (encode-time 0 0 0
                             (string-to-number (cadr parts))
                             (string-to-number (car parts))
                             year)))
      ('weekly (let ((week (string-to-number name)))
                 (encode-time 0 0 0 (1+ (* (1- week) 7)) 1 year)))
      ('monthly (encode-time 0 0 0 1
                             (1+ (seq-position
                                  '("January" "February" "March" "April" "May" "June"
                                    "July" "August" "September" "October" "November" "December")
                                  name #'string=))
                             year)))))

(defun notes-prev ()
  (interactive)
  (let ((type (notes--current-type))
        (time (notes--parse-date)))
    (when type
      (let* ((offset (pcase type
                       ('daily (days-to-time 1))
                       ('weekly (days-to-time 7))
                       ('monthly (days-to-time 30))))
             (new-time (time-subtract time offset))
             (file (pcase type
                     ('daily (notes--daily-file new-time))
                     ('weekly (notes--weekly-file new-time))
                     ('monthly (notes--monthly-file new-time)))))
        (notes--ensure-dir file)
        (find-file file)))))

(defun notes-next ()
  (interactive)
  (let ((type (notes--current-type))
        (time (notes--parse-date)))
    (when type
      (let* ((offset (pcase type
                       ('daily (days-to-time 1))
                       ('weekly (days-to-time 7))
                       ('monthly (days-to-time 30))))
             (new-time (time-add time offset))
             (file (pcase type
                     ('daily (notes--daily-file new-time))
                     ('weekly (notes--weekly-file new-time))
                     ('monthly (notes--monthly-file new-time)))))
        (notes--ensure-dir file)
        (find-file file)))))

(global-set-key (kbd "|") 'notes-open-daily)
(global-set-key (kbd "C-\\") 'notes-open-weekly)
(global-set-key (kbd "C-|") 'notes-open-monthly)
(global-set-key (kbd "M-[") 'notes-prev)
(global-set-key (kbd "M-]") 'notes-next)
#+end_src

* Pieces
#+begin_src emacs-lisp
;; Pieces
(defvar pieces-directory (expand-file-name "pieces" notes-directory))

(defun pieces--find-file (act piece)
  (let* ((act-dir (expand-file-name (format "Act %d" act) pieces-directory))
         (pattern (format "^%d_.*\\.org$" piece))
         (files (and (file-directory-p act-dir)
                     (directory-files act-dir t pattern))))
    (if files
        (car files)
      (expand-file-name (format "%d_.org" piece) act-dir))))

(defun pieces-open-or-capture (act piece)
  (let ((file (pieces--find-file act piece)))
    (pieces--ensure-dir file)
    (if (use-region-p)
        (let ((text (buffer-substring-no-properties (region-beginning) (region-end))))
          (with-current-buffer (find-file-noselect file)
            (goto-char (point-max))
            (insert "\n" text)
            (save-buffer))
          (deactivate-mark)
          (message "Captured to %s" (file-name-nondirectory file)))
      (find-file file))))

(defun pieces--ensure-dir (file)
  (let ((dir (file-name-directory file)))
    (unless (file-exists-p dir)
      (make-directory dir t))))

(defun pieces--make-binding (act piece)
  (let ((fn (lambda () (interactive) (pieces-open-or-capture act piece))))
    (defalias (intern (format "pieces-open-%d-%d" act piece)) fn)
    (intern (format "pieces-open-%d-%d" act piece))))

(defun pieces-search ()
  (interactive)
  (let* ((files (directory-files-recursively pieces-directory "\\.org$"))
         (names (mapcar (lambda (f) (file-relative-name f pieces-directory)) files))
         (selected (completing-read "Piece: " names nil t))
         (file (expand-file-name selected pieces-directory)))
    (if (use-region-p)
        (let ((text (buffer-substring-no-properties (region-beginning) (region-end))))
          (with-current-buffer (find-file-noselect file)
            (goto-char (point-max))
            (insert "\n" text)
            (save-buffer))
          (deactivate-mark)
          (message "Captured to %s" selected))
      (find-file file))))

;; Piece tags
(defun pieces--name-to-tag (filename)
  "Convert '3_The Big Fight.org' to 'TheBigFight'."
  (let* ((base (file-name-sans-extension (file-name-nondirectory filename)))
         (name (if (string-match "^[0-9]+_\\(.*\\)" base)
                   (match-string 1 base)
                 base)))
    (replace-regexp-in-string " " "" name)))

(defun pieces--tag-to-file (tag)
  "Find file matching tag 'TheBigFight'."
  (let ((files (directory-files-recursively pieces-directory "\\.org$")))
    (seq-find (lambda (f) (string= tag (pieces--name-to-tag f))) files)))

(defun pieces--all-tags ()
  "Get all piece tags."
  (let ((files (directory-files-recursively pieces-directory "\\.org$")))
    (mapcar #'pieces--name-to-tag files)))

(defun pieces-update-tags ()
  "Update org-tag-alist with piece tags."
  (interactive)
  (setq org-tag-alist
        (mapcar (lambda (tag) (cons tag ?p)) (pieces--all-tags)))
  (message "Updated %d piece tags" (length org-tag-alist)))

(defun pieces-goto-tag-at-point ()
  "Jump to piece file for tag at point."
  (interactive)
  (let* ((tag (org-get-tags nil t))
         (piece-tags (pieces--all-tags))
         (found (seq-find (lambda (t) (member t piece-tags)) tag)))
    (if found
        (find-file (pieces--tag-to-file found))
      (message "No piece tag at point"))))

(defun pieces-smart-jump ()
  "Jump to piece if in tagged heading, otherwise search pieces."
  (interactive)
  (if (and (derived-mode-p 'org-mode)
           (org-at-heading-p))
      (let* ((tags (org-get-tags nil t))
             (piece-tags (pieces--all-tags))
             (found (seq-find (lambda (tg) (member tg piece-tags)) tags)))
        (if found
            (find-file (pieces--tag-to-file found))
          (pieces-search)))
    (pieces-search)))

(defun pieces-show-backlinks ()
  "Show all daily entries tagged with current piece."
  (interactive)
  (let* ((tag (pieces--name-to-tag buffer-file-name))
         (daily-dir (expand-file-name "daily" notes-directory)))
    (consult-ripgrep daily-dir (format ":%s:" tag))))

;; Auto-update tags when entering org-mode in notes
(add-hook 'org-mode-hook
          (lambda ()
            (when (and buffer-file-name
                       (string-prefix-p (expand-file-name notes-directory)
                                        (expand-file-name buffer-file-name)))
              (pieces-update-tags))))
#+end_src

* Org Packages
#+begin_src emacs-lisp
(use-package org-modern
  :ensure t
  :hook (org-mode . org-modern-mode)
  :config
  (set-face-attribute 'org-block-end-line nil :foreground "#000000"))

(use-package org-appear
  :ensure t
  :hook (org-mode . org-appear-mode)
  :config
  (setq org-appear-autoemphasis t)
  (setq org-appear-autolinks t)
  (setq org-appear-autosubmarkers t))


(use-package olivetti
  :ensure t
  :hook (org-mode . olivetti-mode)
  :config
  (setq olivetti-body-width 100)
  (set-face-attribute 'olivetti-fringe nil :background "#000000"))

(use-package org-fragtog
  :ensure t
  :hook (org-mode . org-fragtog-mode))

(use-package org-transclusion
  :ensure t
  :after org
  :config
  (set-face-attribute 'org-transclusion-fringe nil :foreground "dim gray")
  (set-face-attribute 'org-transclusion nil
                      :background nil
                      :foreground "gray60"))
#+end_src

* Transclusions
#+begin_src emacs-lisp
;; Auto-transclusion for periodic notes
(defun notes--week-days (week-num year)
  "Return list of (month day) pairs for a given ISO week."
  (let* ((jan4 (encode-time 0 0 0 4 1 year))
         (jan4-dow (string-to-number (format-time-string "%u" jan4)))
         (week1-mon (time-subtract jan4 (days-to-time (1- jan4-dow))))
         (week-mon (time-add week1-mon (days-to-time (* 7 (1- week-num)))))
         (days '()))
    (dotimes (i 7)
      (let ((day (time-add week-mon (days-to-time i))))
        (push (cons (string-to-number (format-time-string "%m" day))
                    (string-to-number (format-time-string "%d" day)))
              days)))
    (nreverse days)))

(defun notes--month-weeks (month-name year)
  "Return list of week numbers that have days in the given month."
  (let* ((months '("January" "February" "March" "April" "May" "June"
                   "July" "August" "September" "October" "November" "December"))
         (month-num (1+ (seq-position months month-name #'string=)))
         (weeks '()))
    (dotimes (week 53)
      (let ((days (notes--week-days (1+ week) year)))
        (when (seq-find (lambda (d) (= (car d) month-num)) days)
          (push (1+ week) weeks))))
    (nreverse weeks)))

(defun notes--get-first-heading (file)
  "Get the first heading text from FILE for transclusion target."
  (with-temp-buffer
    (insert-file-contents file)
    (goto-char (point-min))
    (when (re-search-forward "^\\*+[ \t]+\\(.*\\)$" nil t)
      (match-string 1))))

(defun notes--update-transclusions ()
  "Update transclusion links in weekly/monthly notes."
  (when buffer-file-name
    (let* ((name (file-name-sans-extension (file-name-nondirectory buffer-file-name)))
           (dir (file-name-nondirectory (directory-file-name (file-name-directory buffer-file-name))))
           (year (string-to-number (format-time-string "%Y"))))
      (cond
       ;; Weekly note - transclude daily notes (first heading only)
       ((string= dir "weekly")
        (let* ((week-num (string-to-number name))
               (days (notes--week-days week-num year))
               (daily-dir (expand-file-name "daily" notes-directory))
               (links '()))
          (dolist (day days)
            (let ((file (expand-file-name (format "%02d-%02d.org" (car day) (cdr day)) daily-dir)))
              (when (file-exists-p file)
                (let ((heading (notes--get-first-heading file)))
                  (when heading
                    (push (format "#+transclude: [[file:%s::*%s]]" file heading) links))))))
          (notes--replace-transclusions (nreverse links))))
       ;; Monthly note - transclude weekly notes (first heading only)
       ((string= dir "monthly")
        (let* ((weeks (notes--month-weeks name year))
               (weekly-dir (expand-file-name "weekly" notes-directory))
               (links '()))
          (dolist (week weeks)
            (let ((file (expand-file-name (format "%02d.org" week) weekly-dir)))
              (when (file-exists-p file)
                (let ((heading (notes--get-first-heading file)))
                  (when heading
                    (push (format "#+transclude: [[file:%s::*%s]]" file heading) links))))))
          (notes--replace-transclusions (nreverse links))))))))

(defun notes--replace-transclusions (links)
  "Replace transclusion block in current buffer."
  (save-excursion
    (goto-char (point-min))
    (let ((start (if (re-search-forward "^# BEGIN TRANSCLUSIONS$" nil t)
                     (line-beginning-position)
                   nil))
          (end (if (re-search-forward "^# END TRANSCLUSIONS$" nil t)
                   (line-end-position)
                 nil)))
      (if (and start end)
          (delete-region start (1+ end))
        (goto-char (point-max))))
    (goto-char (point-max))
    (unless (bolp) (insert "\n"))
    (insert "# BEGIN TRANSCLUSIONS\n")
    (dolist (link links)
      (insert link "\n"))
    (insert "# END TRANSCLUSIONS\n")
    (save-buffer)))

(defun notes--periodic-note-p ()
  (when buffer-file-name
    (let ((dir (file-name-nondirectory (directory-file-name (file-name-directory buffer-file-name)))))
      (member dir '("weekly" "monthly")))))

(add-hook 'find-file-hook
          (lambda ()
            (when (notes--periodic-note-p)
              (notes--update-transclusions)
              (org-transclusion-mode 1))))
#+end_src

* Completion
#+begin_src emacs-lisp
;;; Completion (minibuffer)
(use-package vertico
  :ensure t
  :config
  (vertico-mode 1)
  (setq vertico-count 15)
  (setq vertico-resize nil)
  (require 'vertico-repeat)
  (add-hook 'minibuffer-setup-hook #'vertico-repeat-save))

(use-package orderless
  :ensure t
  :config
  (setq completion-styles '(orderless basic))
  (setq completion-category-overrides nil)
  (setq completion-category-defaults nil))

(use-package marginalia
  :ensure t
  :config
  (marginalia-mode 1))

(use-package consult
  :ensure t
  :bind (("C-s" . consult-line)
         ("C-x b" . consult-buffer))
  :config
  (setq consult-preview-key 'any)
  (setq consult-async-min-input 0)
  (setq consult-async-refresh-delay 0.1)
  (setq consult-async-input-debounce 0.05)
  (setq consult-async-input-throttle 0.1))
#+end_src

* Navigation
#+begin_src emacs-lisp
;;; Navigation
(use-package avy
  :ensure t)

(use-package nerd-icons
  :ensure t)

(use-package dirvish
  :ensure t
  :after nerd-icons
  :init
  (dirvish-override-dired-mode)
  :config
  (setq dirvish-attributes '(nerd-icons file-size collapse subtree-state vc-state git-msg))
  (setq dirvish-mode-line-format '(:left (sort symlink) :right (omit yank index)))
  (setq dirvish-side-width 35)
  :bind
  (:map dirvish-mode-map
        ("a" . dirvish-quick-access)
        ("TAB" . dirvish-subtree-toggle)
        ("q" . dirvish-quit)))

(use-package vi-tilde-fringe
  :ensure t
  :hook (prog-mode . vi-tilde-fringe-mode))
#+end_src

* In-buffer Completion
#+begin_src emacs-lisp
;;; Completion (in-buffer)
(use-package corfu
  :ensure t
  :config
  (setq corfu-auto t)
  (setq corfu-auto-delay 0.2)
  (setq corfu-auto-prefix 3)
  (global-corfu-mode 1)
  (add-hook 'org-mode-hook (lambda () (corfu-mode -1))))

(use-package cape
  :ensure t
  :init
  (add-to-list 'completion-at-point-functions #'cape-file))
#+end_src

* File Locking
#+begin_src emacs-lisp
;;; File locking
(defun toggle-file-lock ()
  "Toggle write permission on current file."
  (interactive)
  (if-let ((file (buffer-file-name)))
      (let* ((modes (file-modes file))
             (writable (file-writable-p file)))
        (if writable
            (set-file-modes file (logand modes #o555))
          (set-file-modes file (logior modes #o200)))
        (revert-buffer t t t)
        (message "File %s" (if writable "locked" "unlocked")))
    (message "Buffer has no file")))
#+end_src

* Clipboard
#+begin_src emacs-lisp
;;; Wayland clipboard
(setq interprogram-cut-function
      (lambda (text)
        (call-process "wl-copy" nil 0 nil text)))

(setq interprogram-paste-function
      (lambda ()
        (when (zerop (call-process "wl-paste" nil nil nil "-n"))
          (with-temp-buffer
            (call-process "wl-paste" nil t nil "-n")
            (buffer-string)))))
#+end_src

* Evil
#+begin_src emacs-lisp
;;; Evil
(use-package evil
  :ensure t
  :init
  (setq evil-want-C-u-scroll t)
  (setq evil-want-Y-yank-to-eol t)
  (setq evil-want-keybinding nil)
  (setq evil-undo-system 'undo-redo)
  (setq evil-disable-insert-state-bindings t)
  :config
  (evil-mode 1)
  ;; Helix-style g bindings for LSP
  (evil-define-key 'normal 'global
    "gd" 'xref-find-definitions
    "gy" 'eglot-find-typeDefinition
    "gr" 'xref-find-references
    "gi" 'eglot-find-implementation))

(use-package evil-collection
  :ensure t
  :after evil
  :config
  (evil-collection-init))

(use-package evil-surround
  :ensure t
  :after evil
  :config
  (global-evil-surround-mode 1))

(use-package evil-commentary
  :ensure t
  :after evil
  :config
  (evil-commentary-mode 1))

(use-package evil-snipe
  :ensure t
  :after evil
  :config
  (evil-snipe-mode 1)
  (evil-snipe-override-mode 1))

(use-package evil-matchit
  :ensure t
  :after evil
  :config
  (global-evil-matchit-mode 1))

(use-package evil-visualstar
  :ensure t
  :after evil
  :config
  (global-evil-visualstar-mode 1))

(use-package evil-numbers
  :ensure t
  :after evil
  :bind (:map evil-normal-state-map
              ("C-a" . evil-numbers/inc-at-pt)
              ("C-x" . evil-numbers/dec-at-pt)))
#+end_src

* Which-key
#+begin_src emacs-lisp
;;; Which-key
(use-package which-key
  :ensure t
  :config
  (setq which-key-idle-delay 0.3)
  (setq which-key-show-prefix 'echo)
  (setq which-key-prefix-prefix "")
  (setq which-key-echo-keystrokes 0)
  (defvar my/colors '((path . "#61afef")
                      (modified . "#e5c07b")
                      (branch . "#a1d6e2")
                      (ahead . "#98c379")
                      (behind . "#e5c07b")
                      (staged . "#98c379")
                      (unstaged . "#e06c75")))

  (defun my/color (key)
    (alist-get key my/colors))

  (defun my/git-status ()
    "Get p10k-style git status string with colors."
    (when-let* ((file (buffer-file-name))
                ((vc-backend file))
                (branch (car (vc-git-branches)))
                (default-directory (file-name-directory file))
                (out (shell-command-to-string "git status --porcelain -b 2>/dev/null")))
      (let ((ahead 0) (behind 0) (staged 0) (unstaged 0))
        (dolist (line (split-string out "\n" t))
          (cond
           ((string-match "ahead \\([0-9]+\\)" line) (setq ahead (string-to-number (match-string 1 line))))
           ((string-match "behind \\([0-9]+\\)" line) (setq behind (string-to-number (match-string 1 line))))
           ((string-match "^[MADRC]" line) (setq staged (1+ staged)))
           ((string-match "^.[MADRC]" line) (setq unstaged (1+ unstaged)))))
        (concat (propertize branch 'face `(:foreground ,(my/color 'branch)))
                (if (> ahead 0) (propertize (format " ↑%d" ahead) 'face `(:foreground ,(my/color 'ahead))) "")
                (if (> behind 0) (propertize (format " ↓%d" behind) 'face `(:foreground ,(my/color 'behind))) "")
                (if (> staged 0) (propertize (format " +%d" staged) 'face `(:foreground ,(my/color 'staged))) "")
                (if (> unstaged 0) (propertize (format " !%d" unstaged) 'face `(:foreground ,(my/color 'unstaged))) "")))))

  (advice-add 'which-key--echo :override
              (lambda (&rest _)
                (let* ((path (propertize (abbreviate-file-name (or (buffer-file-name) (buffer-name)))
                                         'face `(:foreground ,(my/color 'path))))
                       (mod (cond (buffer-read-only " %%") ((buffer-modified-p) " *") (t nil)))
                       (mod-str (if mod (propertize mod 'face `(:foreground ,(my/color 'modified))) ""))
                       (git (my/git-status)))
                  (message "%s%s%s" path mod-str (if git (format " %s" git) "")))))
  (dolist (n (number-sequence 1 9))
    (push `((,(format "SPC %d" n) . nil) . t) which-key-replacement-alist))
  (which-key-mode 1))
#+end_src

* Leader Key
#+begin_src emacs-lisp
;;; General (leader key) - Helix-style space mode
(use-package general
  :ensure t
  :after evil
  :config
  (general-create-definer leader-def
    :states '(normal visual motion)
    :keymaps 'override
    :prefix "SPC")

  (leader-def
    "" '(nil :which-key "space")
    "SPC" '(project-find-file :which-key "find file (project)")
    "\\" '(pieces-smart-jump :which-key "pieces")
    "|" '(restart-emacs :which-key "restart")
    "TAB" '(vterm :which-key "terminal")

    ;; Doom-style punctuation
    "." '(consult-fd :which-key "find file")
    "," '(consult-buffer :which-key "switch buffer")
    ";" '(execute-extended-command :which-key "M-x")
    "x" '(scratch-buffer :which-key "scratch")

    ;; File/buffer pickers
    "f" '(project-find-file :which-key "file picker (project)")
    "F" '(find-file :which-key "file picker (cwd)")
    "b" '(consult-buffer :which-key "buffer picker")
    "j" '(evil-show-jumps :which-key "jumplist picker")

    ;; LSP
    "k" '(eldoc-box-help-at-point :which-key "hover docs")
    "s" '(consult-imenu :which-key "document symbols")
    "S" '(xref-find-apropos :which-key "workspace symbols")
    "d" '(consult-flymake :which-key "diagnostics (buffer)")
    "D" '(flymake-show-project-diagnostics :which-key "diagnostics (project)")
    "r" '(eglot-rename :which-key "rename symbol")
    "a" '(eglot-code-actions :which-key "code action")
    "h" '(xref-find-references :which-key "references")

    ;; Misc
    "'" '(vertico-repeat :which-key "last picker")
    "c" '(evil-commentary-line :which-key "comment")
    "l" '(toggle-file-lock :which-key "lock/unlock file")
    "C" '((lambda () (interactive) (consult-fd "~/.config")) :which-key "find in ~/.config")

    ;; Clipboard
    "p" '(clipboard-yank :which-key "paste clipboard")
    "y" '(clipboard-kill-ring-save :which-key "yank to clipboard")
    "R" '((lambda () (interactive)
            (delete-region (region-beginning) (region-end))
            (clipboard-yank)) :which-key "replace with clipboard")

    ;; Search
    "/" '(consult-ripgrep :which-key "global search")
    "?" '(execute-extended-command :which-key "command palette")

    ;; Window mode
    "w" '(:ignore t :which-key "window")
    "ww" '(other-window :which-key "other")
    "wv" '(split-window-right :which-key "vsplit")
    "ws" '(split-window-below :which-key "split")
    "wd" '(delete-window :which-key "close")
    "wh" '(windmove-left :which-key "left")
    "wj" '(windmove-down :which-key "down")
    "wk" '(windmove-up :which-key "up")
    "wl" '(windmove-right :which-key "right")

    ;; Quit
    "q" '(save-buffers-kill-terminal :which-key "quit"))

  ;; Generate piece bindings for each act (hidden from which-key)
  (dolist (act (number-sequence 1 9))
    (dolist (piece (number-sequence 1 9))
      (let ((fn (pieces--make-binding act piece))
            (key (format "%d%d" act piece)))
        (general-define-key
         :states '(normal visual motion)
         :keymaps 'override
         :prefix "SPC"
         :wk-full-keys nil
         key `(,fn :which-key nil))))))
#+end_src

* Terminal
#+begin_src emacs-lisp
;;; Terminal
(use-package vterm
  :ensure t
  :config
  (setq vterm-module-cmake-args "-DUSE_SYSTEM_LIBVTERM=yes"))
#+end_src

* Git
#+begin_src emacs-lisp
;;; Magit
(use-package magit
  :ensure t
  :bind (("C-x g" . magit-status)
         ("M-g" . magit-status)))

(use-package diff-hl
  :ensure t
  :hook ((prog-mode . diff-hl-mode)
         (text-mode . diff-hl-mode)
         (magit-pre-refresh . diff-hl-magit-pre-refresh)
         (magit-post-refresh . diff-hl-magit-post-refresh))
  :config
  (diff-hl-flydiff-mode 1))
#+end_src

* Eglot
#+begin_src emacs-lisp
;;; Eglot + Languages
(add-hook 'eglot-managed-mode-hook #'eglot-inlay-hints-mode)
(add-hook 'eglot-managed-mode-hook #'flymake-mode)
(setq flymake-fringe-indicator-position nil)

(with-eval-after-load 'eglot
  (add-to-list 'eglot-server-programs '(toml-ts-mode . ("taplo" "lsp" "stdio")))
  (add-to-list 'eglot-server-programs '(yaml-mode . ("yaml-language-server" "--stdio")))
  (add-to-list 'eglot-server-programs '(yaml-ts-mode . ("yaml-language-server" "--stdio")))
  (setq eglot-report-progress nil)
  (advice-add 'eglot--message :override #'ignore))

(use-package eldoc-box
  :ensure t)

(use-package eglot-booster
  :vc (:url "https://github.com/jdtsmith/eglot-booster")
  :after eglot
  :config
  (eglot-booster-mode))
#+end_src

* Languages
#+begin_src emacs-lisp
(use-package go-mode
  :ensure t
  :hook ((go-mode go-ts-mode) . eglot-ensure)
  :config
  (add-hook 'before-save-hook
            (lambda ()
              (when (derived-mode-p 'go-mode 'go-ts-mode)
                (eglot-format-buffer)
                (eglot-code-action-organize-imports (point-min) (point-max))))))

;; Python (uses pyright)
(setq python-indent-guess-indent-offset-verbose nil)
(add-hook 'python-mode-hook 'eglot-ensure)
(add-hook 'python-ts-mode-hook 'eglot-ensure)

;; Shell (uses bash-language-server)
(add-hook 'sh-mode-hook 'eglot-ensure)
(add-hook 'bash-ts-mode-hook 'eglot-ensure)

;; Web development
(use-package web-mode
  :ensure t
  :mode ("\\.html\\'" "\\.jsx\\'" "\\.tsx\\'" "\\.vue\\'")
  :hook (web-mode . eglot-ensure)
  :config
  (setq web-mode-markup-indent-offset 2)
  (setq web-mode-css-indent-offset 2)
  (setq web-mode-code-indent-offset 2))

(use-package typescript-mode
  :ensure t
  :mode "\\.ts\\'"
  :hook ((typescript-mode typescript-ts-mode) . eglot-ensure)
  :config
  (setq typescript-indent-level 2))

(add-hook 'tsx-ts-mode-hook 'eglot-ensure)

(use-package js2-mode
  :ensure t
  :mode "\\.js\\'"
  :hook ((js2-mode js-ts-mode) . eglot-ensure)
  :config
  (setq js2-basic-offset 2))

;; CSS (built-in)
(add-hook 'css-mode-hook 'eglot-ensure)
(add-hook 'css-ts-mode-hook 'eglot-ensure)

;; JSON (built-in)
(add-hook 'json-mode-hook 'eglot-ensure)
(add-hook 'json-ts-mode-hook 'eglot-ensure)

;; C/C++ (uses clangd)
(add-hook 'c-mode-hook 'eglot-ensure)
(add-hook 'c-ts-mode-hook 'eglot-ensure)
(add-hook 'c++-mode-hook 'eglot-ensure)
(add-hook 'c++-ts-mode-hook 'eglot-ensure)

;; TOML & YAML
(add-hook 'toml-ts-mode-hook 'eglot-ensure)
(add-hook 'yaml-mode-hook 'eglot-ensure)
(add-hook 'yaml-ts-mode-hook 'eglot-ensure)

;; Lua
(use-package lua-mode
  :ensure t
  :hook (lua-mode . eglot-ensure)
  :config
  (setq lua-indent-level 2))

;; Nix
(use-package nix-mode
  :ensure t
  :mode "\\.nix\\'")
#+end_src

* Treesitter
#+begin_src emacs-lisp
;;; Treesitter
(use-package treesit-auto
  :ensure t
  :config
  (setq treesit-auto-install 'prompt)
  (global-treesit-auto-mode 1))
#+end_src
